### 在代码段中使用数据

编程计算8个数据的和,结果保存在ax中

```nasm
assume cs:code
code segment

    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
    mov ax,0
    mov bx,0

    mov cx,8
    s:
    add ax,cs:[bx]
    add bx,2
    loop s

    mov ax,4c00h
    int 21h

code ends
end
```

`dw`即`define word`,定义**字型**数据,程序在运行时`CS`中存放代码,所以这8个数据存放在`CS`中,位于代码段的开始,所以偏移地址为0

!>由于是字型数据,所以`add bx,2`

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/dw_1.png)

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/dw_2.png)

debug加载后的的`IP`为`0000`,要将其修改为`0010`才能正常运行程序,因此存在缺陷

```nasm
assume cs:code
code segment

    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
    start:
    mov ax,0
    mov bx,0

    mov cx,8
    s:
    add ax,cs:[bx]
    add bx,2
    loop s

    mov ax,4c00h
    int 21h

code ends
end start
```

在程序的第一条指令的前面加上一个标号`start`,并在伪指令`end`后面出现

!>`end`除了通知编译器程序结束外,还可以通知编译器程序的入口在什么地方,在不指明程序入口的情况下,程序默认按照顺序从头开始执行

因此`mov bx,0`是程序的第一条指令

### 在代码段中使用栈

利用栈,将程序中定义的数据逆序存放

```nasm
assume cs:code
code segment

    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
    dw 0,0,0,0,0,0,0,0;用dw定义了16个字型数据,在程序加载后,将这16个字的内存空间作为栈来使用
    start:
    mov ax,cs
    mov ss,ax
    mov sp,20h;将栈顶指向cs:20

    mov bx,0
    mov cx,8
    s0:
    push cs:[bx]
    add bx,2
    loop s0
    
    mov bx,0
    mov cx,8
    s1:
    pop cs:[bx]
    add bx,2
    loop s1

    mov ax,4c00h
    int 21h

code ends
end start
```

### 将数据,代码,栈放入不同的段

将程序中定义的数据逆序存放

```nasm
assume cs:code,ds:data,ss:stack

data segment
    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
data ends

stack segment
    dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
stack ends

code segment
    start:
    mov ax,stack
    mov ss,ax
    mov sp,20h;设置栈顶ss:sp指向stack:20

    mov ax,data
    mov ds,ax
    mov bx,0;ds:bx指向data段中的第一个单元

    mov cx,8
    s0:
    push [bx]
    add bx,2
    loop s0

    mov bx,0

    mov cx,8
    s1:
    pop [bx]
    add bx,2
    loop s1
    
    mov ax,4c00h
    int 21h
code ends

end start
```

设置了三个段`assume cs:code,ds:data,ss:stack`,其后将段寄存器指向这些段

### 实验5 编写,调试具有多个段的程序

```nasm
assume cs:code,ds:data,ss:stack

data segment
   ...
data ends

stack segment
   ...
stack ends

code segment

start:  
   ...

code ends

end start
```

!>数据段空间大小为定义数据所需的16字节的最小整数倍

比如定义了1个字节,系统就给数据段分配16个字节;定义了17个字节,系统就分配32个字节

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/实验5_1.png)

---

```nasm
assume cs:code,ds:data,ss:stack

code segment
    start:
    mov ax,stack
    mov ss,ax
    mov sp,16

    mov ax,data
    mov ds,ax

    push ds:[0]
    push ds:[2]
    pop ds:[2]
    pop ds:[0]
    
    mov ax,4c00h
    int 21h
code ends

data segment
    dw 0123h,0456h
data ends

stack segment
    dw 0,0
stack ends

end start
```

设程序加载后,code段的段地址为`X`,则data段的段地址为`X+3`,stack段的段地址为`X+4`

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/实验5_2.png)