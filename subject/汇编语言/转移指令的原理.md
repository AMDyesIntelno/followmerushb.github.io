!>可以修改`IP`或同时修改`CS`和`IP`的指令统称为转移指令

- 只修改`IP`时,称为段内转移
- 同时修改`CS`和`IP`时,称为段间转移

### offset

!>操作符`offset`是由编译器处理的符号,其功能时取得标号的偏移地址

```nasm
assume cs:codesg
codesg segment
    start:
    mov ax,offset start;相当于mov ax,0
    s:
    mov ax,offset s;相当于mov ax,3,前一条指令的长度为3
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/offset_1.png)

```nasm
assume cs:codesg
codesg segment
    s:
    mov ax,bx;占两个字节
    mov si,offset s
    mov di,offset s0
    mov ax,cs:[si]
    mov cs:[di],ax
    s0:
    nop;nop的机器码占一个字节
    nop
codesg ends
end s
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/offset_2.png)

### jmp

!>jmp为无条件转移指令,可以只修改`IP`也可以同时修改`CS`和`IP`

jmp指令要给出以下信息

- 转移的目的地址
- 转移的距离(段间转移,段内短转移,段内近转移)

#### 依据位移进行转移

`jmp short 标号`->IP=IP+8位位移(段内短转移),对IP的修改范围是`-128~127`

```nasm
assume cs:codesg
codesg segment
    start:
    mov ax,0
    jmp short s
    add ax,1
    s:
    inc ax
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/jmp_1.png)

```nasm
assume cs:codesg
codesg segment
    start:
    mov ax,0
    mov bx,0
    jmp short s
    add ax,1
    s:
    inc ax
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/jmp_2.png)

```nasm
assume cs:codesg
codesg segment
    start:
    mov ax,0
    mov bx,0
    jmp short s
    add ax,1
    add ax,1
    s:
    inc ax
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/jmp_3.png)

```nasm
assume cs:codesg
codesg segment
    s:
    inc ax
    start:
    mov ax,0
    mov bx,0
    jmp short s
    add ax,1
    add ax,1
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/jmp_4.png)

!>在前移时,计算偏移时需要计算`jmp`指令的长度

`inc ax`1个字节,`mov ax,0`3个字节,`jmp short`2个字节,一共9个字节,`00-9=F7`,所以得出`jmp short s`的内存为`EBF7`

!>CPU在执行jmp指令时并不需要转移的目的地址

- CS=07E4,IP=0006,CS:IP指向`EB ??`(jmp short s的机器码)
- 读取指令(`EB ??`)进入指令缓冲器
- IP=IP+所读取的指令的长度,CS:IP指向`add ax,1`
- CPU执行指令缓冲器中的指令
- 指令执行后,IP指向`inc ax`

!>指令`EB ??`中没有指出要转移的目的地址,而是指出了要转移的位移

`jmp near ptr 标号`->IP=IP+16位位移(段内近转移),对IP的修改范围是`-32768~32767`

```nasm
assume cs:codesg
codesg segment
    start:
    mov ax,0
    mov bx,0
    jmp near ptr s
    db 128 dup (0)
    add ax,1
    s:
    inc ax
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/jmp_5.png)

`jmp far ptr 标号`->CS=标号所在段的段地址,IP=标号所在段中的偏移地址

```nasm
assume cs:codesg
codesg segment
    start:
    mov ax,0
    mov bx,0
    jmp far ptr s
    db 128 dup (0)
    add ax,1
    s:
    inc ax
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/jmp_6.png)

`8E 00 E4 07`是目的地址在指令中的存储顺序,**高地址**的`E4 07`是转移的**段地址**`07E4`,**低地址**的`8E 00`是**偏移地址**`008E`

`jmp 16位reg`->IP=16位reg中的值

#### 转移地址在内存中

- `jmp word ptr 内存单元`(段内转移)

内存单元存储的字作为转移目的的偏移地址

```nasm
mov ax,0123h
mov ds:[0],ax
jmp word ptr ds:[0];IP=0123h
```

- `jmp dword ptr 内存单元`(段间转移)

**高地址**处的字是转移的目的**段地址**,**低地址**处的字是**偏移地址**

```nasm
mov ax,0123h
mov ds:[0],ax
mov word ptr ds:[2],0
jmp dword ptr ds:[0];CS=0,IP=0123h
```

### jcxz/loop

!>jcxz指令为有条件转移指令,所有的有条件转移指令都是短转移

`jcxz 标号`(如果cx=0,则转移到标号处执行)

!>loop指令为循环指令,所有的循环指令都是短转移

`loop 标号`(cx=cx-1,如果cx!=0,转移到标号处执行)

### 实验8 分析程序

```nasm
assume cs:codesg
codesg segment
    mov ax, 4c00h
    int 21h
    start:
    mov ax, 0
    s:
    nop
    nop
    mov di, offset s
    mov si, offset s2
    mov ax, cs:[si]
    mov cs:[di], ax
    s0:
    jmp short s
    s1:
    mov ax, 0
    int 21h
    mov ax, 0
    s2:
    jmp short s1
    nop
codesg ends
end start
```

1. 程序开始,定位到`start`,执行`mov ax,0`
2. `mov di,offset s`,向di中存储`s`相对于`codesg`的偏移地址
3. `mov si,offset s2`,向si中存储`s2`相对于`codesg`的偏移地址
4. `mov ax,cs:[si]`,向ax中存储cs:[si]所保存的字型数据,即标号s2处的数据
5. `jmp short s1`的内存数据为`EBF6`(`mov ax,0`3个字节,`int 21h`2个字节,`jmp short s1`2个字节,一共10个字节,`00-0A=F6`,所以`jmp short s1`的内存为`EBF6`)
6. `mov cs:[di], ax`,s的两个nop被替换为`EBF6`
7. 执行s0,重新跳转到s
8. 执行`EBF6`,相当于向前跳转10个字节(`mov ax,4c00h`3个字节,`int 21h`2个字节,`mov ax,0`3个字节,`EBF6`2个字节),所以跳转到`mov ax, 4c00h`
9. 继续执行程序,正常结束

### 实验9 根据材料编程

```nasm
assume cs:codesg
data segment
    db 'welcome to masm!'
data ends
codesg segment
    start:
    mov ax,0b800h
    mov ds,ax
    mov ax,data
    mov es,ax
    mov si,0
    mov bx,07c0h
    
    mov ah,00000010b;welcome
    mov cx,7
    s0:
    mov al,byte ptr es:[si]
    mov word ptr ds:[bx],ax
    inc si
    add bx,2
    loop s0

    mov ah,00000000b;空格
    mov al,byte ptr es:[si]
    mov word ptr ds:[bx],ax
    inc si
    add bx,2

    mov ah,00100100b;to
    mov cx,2
    s1:
    mov al,byte ptr es:[si]
    mov word ptr ds:[bx],ax
    inc si
    add bx,2
    loop s1

    mov ah,00000000b;空格
    mov al,byte ptr es:[si]
    mov word ptr ds:[bx],ax
    inc si
    add bx,2

    mov ah,01110001b;masm!
    mov cx,5
    s2:
    mov al,byte ptr es:[si]
    mov word ptr ds:[bx],ax
    inc si
    add bx,2
    loop s2

    mov ax, 4c00h
    int 21h
codesg ends
end start
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/实验9.png)