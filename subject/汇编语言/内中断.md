|中断源|中断类型码|
|:---:|:---:|
|除法错误|0|
|单步执行|1|
|into指令|4|
|int n|n|

### 中断向量表

中断向量表在内存中保存,其中存放着`256`个中断源所对应的中断处理程序的入口

对于8086,中断向量表指定放在内存地址0处,从内存`0000:0000`到`0000:03FF`的`1024`个单元中存放着中断向量表

一个表项存放一个中断向量,一个表项占两个字,**高地址字存放段地址,低地址字存放偏移地址**

存储N号中断源对应的中断处理程序入口的**偏移地址**的内存单元地址为`4N`

存储N号中断源对应的中断处理程序入口的**段地址**的内存单元地址为`4N+2`

用Debug查看内存,情况如下:

```
0000:0000 68 10 A7 00 8B 01 70 00-16 00 9D 03 8B 01 70 00
```

则3号中断源对应的中断处理程序的入口地址为`0070:018B`

### 中断过程

1. 从中断信息中取得中断类型码

2. 将标志寄存器的值入栈`pushf`(中断过程会改变标志寄存器的值)

3. 将标志寄存器的TF和IF置为0`TF=0,IF=0`

4. CS入栈`push CS`

5. IP入栈`push IP`

6. 在中断向量表中读取中断处理程序的入口并设置CS和IP`IP=N*4,CS=N*4+2`

### iret指令

iret指令将IP,CS和标志寄存器出栈`pop IP,pop CS,popf`

iret指令执行后,CPU回到执行中断处理程序前的执行点继续

### 实验12 编写0号中断的处理程序

编写0号中断的处理程序,使得在除法溢出发生时,在屏幕中间显示字符串"divide error!",然后返回到DOS

1. 编写可以在屏幕中间显示"divide error!"的中断处理程序

2. 将中断处理程序送入内存`0000:0200`处

3. 将中断处理程序的入口地址`0000:0200`存储在中断向量表0号表项中

```nasm
assume cs:code
code segment
main:
    mov ax,cs
    mov ds,ax
    mov si,offset do0;将ds:si指向do0程序

    mov ax,0000h
    mov es,ax
    mov di,0200h;将es:di指向内存0000:0200处

    mov cx,offset do0end-offset do0;将cx设置为do0的代码长度

    cld;DF=0,地址自动增量

    rep movsb

    mov ax,0
    mov es,ax;es指向中断向量表
    mov word ptr es:[0*4],0200h;低地址字存放偏移地址
    mov word ptr es:[0*4+2],0000h;高地址字存放段地址

    mov ax,4c00h
    int 21h

do0:;显示字符串,当do0执行是CS:IP指向0000:0200
    jmp short do0start
    db "divide error!"
do0start:
    mov ax,cs
    mov ds,ax
    mov si,0202h;db "divide error!"的偏移为0202h因为jmp short指令占用了两个字节

    mov ax,0b800h;显存段地址
    mov es,ax
    mov di,12*160+2*34;居中显示

    mov ah,00000010b
    mov cx,13
do0loop:
    mov al,byte ptr ds:[si]
    mov word ptr es:[di],ax
    inc si
    add di,2
    loop do0loop
    
    mov ax,4c00h
    int 21h
do0end:
    nop

code ends
end main
```

1. 首先将`ds:si`指向do0程序,然后将`es:di`指向内存`0000:0200`处

2. 用`offset do0end-offset do0`确定do0代码段的长度,`do0end`填入`nop`指令

3. 用`rep movsb`将`do0`代码段中的内容复制到内存`0000:0200`处

4. 将中断向量表的0号中断指向`0000:0200`

5. 将`db "divide error!"`写在`do0`段中,而非放在独立的`data`段中,防止在执行完程序后,内存被释放,空间被占用

6. `mov si,0202h`字符串的偏移为0202h,因为`jmp short`指令占用了两个字节,而如果写的是`jmp`指令,则偏移为0203h,因为jmp后接了一个nop指令

测试代码

```nasm
assume cs:code
code segment
    mov ax,1001h
    mov bh,1
    div bh

    mov ax,4c00h
    int 21h
code ends
end
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/实验12.png)