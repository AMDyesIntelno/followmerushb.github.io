将实验7中的数据按格式显示出来

```nasm
assume cs:code
data segment
    db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
    db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
    db '1993','1994','1995'

    dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
    dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
    
    dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
    dw 11542,14430,15257,17800
data ends

mem segment
    dw 32 dup (0220h);空格
mem ends

stack segment
    db 32 dup(0)
stack ends

code segment
main:
    mov ax,data
    mov ds,ax
    mov bx,0;年份和收入起始位置
    mov bp,168;人数起始位置

    mov ax,mem
    mov es,ax
    mov si,0;si作为mem的下标

    mov ax,stack
    mov ss,ax
    mov sp,32

    mov ax,0b80ah;显存位置
    push ax

    mov cx,21

main_loop:
    push word ptr ds:[bx+2];'xx'
    push word ptr ds:[bx];'19'
    call year
    mov si,10h
    push word ptr ds:[bx+84];低16位
    push word ptr ds:[bx+86];高16位
    call income
    mov si,20h
    push word ptr ds:[bp]
    call people
    mov si,30h
    push word ptr ds:[bp]
    push word ptr ds:[bx+84];低16位
    push word ptr ds:[bx+86];高16位
    call per_income
    pop ax
    add ax,0ah;显存下一行
    push ax
    call show_str
    call clear_mem
    mov si,0
    add bx,4
    add bp,2
    loop main_loop

    mov ax,4c00h
    int 21h

year:;堆栈传参,word ptr ss:[bp+6]'xx',word ptr ss:[bp+4]'19'
    push bp
    mov bp,sp
    push cx
    mov di,4
    mov cx,4
year_loop:
    mov al,byte ptr ss:[bp+di]
    mov byte ptr es:[si],al
    add si,2
    inc di
    loop year_loop
    pop cx
    mov sp,bp
    pop bp
    ret 4;堆栈平衡

income:;堆栈传参,word ptr ss:[bp+6]低16位,word ptr ss:[bp+4]高16位
    push bp
    mov bp,sp
    push cx
    push bx
    mov bx,10
    mov dx,0
    push dx
    mov ax,word ptr ss:[bp+6]
    mov dx,word ptr ss:[bp+4]
income_loop:
    push ax
    push dx
    call divdw;cs存放余数
    add cx,030h;0->'0'
    push cx;ascii入栈
    mov cx,ax
    or cx,dx
    jcxz income_re
    jmp income_loop
income_re:
    pop cx
    jcxz income_ret
    mov byte ptr es:[si],cl
    add si,2
    jmp income_re
income_ret:
    pop bx
    pop cx
    mov sp,bp
    pop bp
    ret 4

divdw:;堆栈传参
    push bp
    mov bp,sp
    mov ax,word ptr ss:[bp+4];高16位
    mov dx,0;dx清零
    div bx;dx:余数,ax:商
    push ax;结果的高16位
    mov ax,word ptr ss:[bp+6];低16位
    div bx;dx:余数,ax:商(低16位)
    mov cx,dx;cx存放余数
    mov dx,word ptr ss:[bp-2];结果的高16位
    mov sp,bp
    pop bp
    ret 4

people:;堆栈传参
    push bp
    mov bp,sp
    push cx
    push bx
    mov bx,10
    mov dx,0
    push dx
    mov ax,word ptr ss:[bp+4]
people_loop:
    div bx;ax:商,dx:余数
    add dx,030h;0->'0'
    push dx
    mov cx,ax
    jcxz people_re
    mov dx,0
    jmp people_loop
people_re:
    pop cx
    jcxz people_ret
    mov byte ptr es:[si],cl
    add si,2
    jmp people_re
people_ret:
    pop bx
    pop cx
    mov sp,bp
    pop bp
    ret 2;堆栈平衡

per_income:;堆栈传参,word ptr ss:[bp+8]员工数量,word ptr ss:[bp+6]低16位,word ptr ss:[bp+4]高16位
    push bp
    mov bp,sp
    push cx
    push bx
    mov ax,word ptr ss:[bp+6]
    mov dx,word ptr ss:[bp+4]
    div word ptr ss:[bp+8];ax存放人均收入
    mov bx,10
    mov dx,0
    push dx
per_income_loop:
    div bx;ax:商,dx:余数
    add dx,030h;0->'0'
    push dx
    mov cx,ax
    jcxz per_income_re
    mov dx,0
    jmp per_income_loop
per_income_re:
    pop cx
    jcxz per_income_ret
    mov byte ptr es:[si],cl
    add si,2
    jmp per_income_re
per_income_ret:
    pop bx
    pop cx
    mov sp,bp
    pop bp
    ret 6;堆栈平衡

show_str:;堆栈传参
    push bp
    mov bp,sp
    push ds
    mov ax,word ptr ss:[bp+4];每一行显存的起始位置
    mov ds,ax
    push cx
    mov cx,32
    push si
    mov si,0
show_str_loop:
    mov ax,word ptr es:[si]
    mov word ptr ds:[si],ax
    add si,2
    loop show_str_loop
    pop si
    pop cx
    pop ax;恢复原本的ds
    mov ds,ax
    mov sp,bp
    pop bp
    ret

clear_mem:
    push bp
    mov bp,sp
    push si
    push cx
    mov si,0
    mov cx,32
    mov ax,0220h;颜色
clear_loop:
    mov word ptr es:[si],ax
    add si,2
    loop clear_loop
    pop cx
    pop si
    mov sp,bp
    pop bp
    ret
code ends
end main
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/课程设计1.png)