- 用来存储相关指令的某些执行结果
- 用来为CPU执行相关指令提供行为依据
- 用来控制CPU的相关工作方式

DEBUG中使用`RF`修改标志寄存器

|标志名|置位(值为1)|复位(值为0)|
|:---:|:---:|:---:|
|溢出OF|OV|NV|
|方向DF|DN|UP|
|中断IF|EI|DI|
|符号SF|NG|PL|
|零ZF|ZR|NZ|
|辅助进位AF|AC|NA|
|奇偶PF|PE|PO|
|进位CF|CY|NC|

### ZF

零标志位,当结果是零时设置,否则清除

```nasm
mov ax,2
sub ax,2;ZF=1
mov ax,1
and ax,0;ZF=1
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/ZF_1.png)

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/ZF_2.png)

### PF

奇偶标志位,当结果的**最低字节**中1的个数为偶数(包括零)时设置,否则清除(用于奇偶校验)

```nasm
mov ax,0a01h;00001010 00000001
add ax,0100h;00001011 00000001,PF=0
sub ax,0001h;00001011 00000000,PF=1
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/PF.png)

### SF

符号标志位,设置成结果的最高位,也就是带符号数中的最高位(0**非负**1负)

`00000001B`可以看作无符号数1,或者有符号数+1

`10000001B`可以看作无符号数129,或者有符号数-127

```nasm
mov al,10000001B
add al,1;SF=1
mov al,10000001B
add al,01111111B;SF=0
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/SF.png)

### CF

进位标志位,当算术运算在最高位产生了**进位**或**借位**时设置,否则清除,该标志也表示**无符号整数运算**发生了溢出

```nasm
mov al,98H
add al,al;al=30H,CF=1
add al,al;al=60H,CF=0
mov al,97H
sub al,98H;al=FFH,CF=1,向更高位借位
sub al,al;al=0,CF=0
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/CF_1.png)

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/CF_2.png)

### OF

溢出标志位,当结果大于可表示的最大正数或小于可表示的最小负数(不包含符号位)时设置,否则清除

该标志位表示了**有符号数算术**(补码算术)发生了溢出情况

```nasm
mov al,98H;152
add al,al;al=30H,CF=1,OF=1
add al,al;al=60H,CF=0,OF=0
mov al,98
add al,al;al=C4H,CF=0,OF=1
add al,al;al=88H,CF=1,OF=1
mov al,0F0H;-16
add al,088H;-120,CF=1,OF=1
mov al,0F0H;-19
add al,78H;120,CF=1,OF=0
```

### adc指令

adc是带进位加法指令,利用了CF位上记录的值

`adc 操作对象1,操作对象2`

操作对象1=操作对象1+操作对象2+CF

```nasm
mov al,98H
add al,al
adc al,3;al=34H
```

`add ax,bx`等价于`add al,bl  adc ah,bh`

计算`1EF000H+201000H`结果放在AX(高16位)和BX(低16位)中

```nasm
mov ax,001EH
mov bx,0F000H
add bx,1000H
adc ax,0020H
```

计算`1EF0001000H+2010001EF0H`结果放在AX(高16位)和BX(中16位)和CX(低16位)中

```nasm
mov ax,001EH
mov bx,0F000H
mov cx,1000H
add cx,1EF0H
adc bx,1000H
adc ax,0020H
```

### sbb指令

sbb是带借位减法指令,利用CF位上记录的借位值

`sbb 操作对象1,操作对象2`

操作对象1=操作对象1-操作对象2-CF

计算`003E1000H-00202000H`

```nasm
mov bx,1000H
mov ax,003EH
sub bx,2000H
sbb ax,0020H
```

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/sbb.png)

### cmp指令

cmp是比较指令

`cmp 操作对象1,操作对象2`

计算操作对象1-操作对象2,但不保存结果,仅根据计算结果对寄存器进行设置

`cmp ax,ax`,进行`ax-ax`,执行后`ZF=1,PF=1,SF=0,CF=0,OF=0`

```nasm
mov ax,8
mov bx,3
cmp ax,bx
```

`ax=8,ZF=0,PF=1,SF=0,CF=0,OF=0`

`cmp ax,bx`

- 如果`ax=bx`,则`ax-bx=0`,则`ZF=1`
- 如果`ax!=bx`,则`ax-bx!=0`,则`ZF=0`
- 如果`ax<bx`,则`ax-bx`会产生借位,则`CF=1`
- 如果`ax>=bx`,则`ax-bx`不必借位,则`CF=0`
- 如果`ax>bx`,则`ax-bx`既不必借位,结果也不为0,则`CF=0且ZF=0`
- 如果`ax<=bx`,则`ax-bx`可能产生借位,结果可能位0则`CF=1或ZF=1`

cmp指令可以进行有符号数运算和无符号数运算

`cmp ah,bh`

对于有符号数运算,如果`ah<bh`,则`ah-bh`会导致`SF=1`,但`SF=1`并不能说明操作对象1小于操作对象2

假设`ah=22H,bh=A0H`,则`ah-bh=34-(-96)=82H(-126)`,`SF=1`

但显然34大于-96,**如果产生了溢出(OF=1),那么逻辑上的真正结果与溢出导致的实际结果相反**

![](https://cdn.jsdelivr.net/gh/followmerushb/followmerushb.github.io@master/static/subject/汇编语言/cmp.png)

### 条件转移指令

|指令|含义|相关标志位|
|:---:|:---:|:---:|
|je|等于则转移|ZF=1|
|jne|不等于则转移|ZF=0|
|jb|低于则转移|CF=1|
|jnb|**不低于**则转移|CF=0|
|ja|高于则转移|CF=0且ZF=0|
|jna|**不高于**则转移|CF=1或ZF=1|

?>统计data段中数值为8的字节的字数,用ax保存统计结果

```nasm
assume cs:codesg
data segment
    db 8,11,8,1,8,5,63,38
data ends
codesg segment
main:
    mov ax,data
    mov ds,ax
    mov ax,0
    mov bx,0
    mov cx,8
main_loop:
    cmp byte ptr ds:[bx],8
    je add1
    jmp next
add1:
    inc ax
next:
    inc bx
    loop main_loop

    mov ax,4c00h
    int 21h

codesg ends
end main
```

### 串传送指令

移动数据(内存-->内存)

在8086中MOVS指令将`ds:si`指向的内存单元移动到`es:di`,`movsb`字节单元,`movsw`字单元

DF标志位(方向标志位)：DF标志位为0时执行一次加1/2/4,为1时执行一次则减1/2/4,根据Byte/Word/Dword决定

在Move操作结束后,SI与DI寄存器会自动的增加或者减少,而依据则是根据标志寄存器中的`DF`标志位来判断增加还是减少

- 如果`DF`标志位为0,则SI与DI寄存器则自增
- 如果`DF`标志位为1,则SI与DI寄存器则自减

- `CLD(clear direction flag)`该指令使DF=0,使地址自动增量
- `STD(set direction flag)`该指令使DF=1,使地址自动减量

`movsb`,`movsw`常与`rep`指令配合使用,`rep`会根据`cx`的值重复执行后面的指令

`rep movsb`

将data段中的第一个字符串复制到其后面的空间中

```nasm
assume cs:codesg
data segment
    db 'Welcome to masm!'
    db 16 dup (0)
data ends
codesg segment
main:
    mov ax,data
    mov ds,ax
    mov es,ax
    mov si,0
    mov di,10H
    mov cx,16
    cld
    rep movsb

    mov ax,4c00h
    int 21h

codesg ends
end main
```

逆向传送字符串,将F000H段中的最后16个字符复制到data段中

```nasm
assume cs:codesg
data segment
    db 16 dup (0)
data ends
codesg segment
main:
    mov ax,0F000H
    mov ds,ax
    mov ax,data
    mov es,ax
    mov si,0FFFFH
    mov di,0FH
    mov cx,16
    std
    rep movsb

    mov ax,4c00h
    int 21h

codesg ends
end main
```

### pushf和popf

`pushf`的功能是将标志寄存器的值压栈,`popf`从栈中弹出数据,送入标志寄存器中

### 实验11 编写子程序

编写一个子程序,将包含任意字符,以0结尾的字符串中的小写字母转变成大写字母

```nasm
assume cs:codesg
datasg segment
    db "Beginner's All-purpose Symbolic Instruction Code.",0
datasg ends
codesg segment
main:
    mov ax,datasg
    mov ds,ax
    mov si,0
    call letterc

    mov ax,4c00h
    int 21h

letterc:
    mov ch,0
    mov cl,byte ptr ds:[si]
    jcxz letterc_return
    cmp cx,61h;'a'
    jb letterc_loop
    cmp cx,7ah;'z'
    ja letterc_loop
    and byte ptr ds:[si],11011111b
letterc_loop:
    inc si
    jmp letterc
letterc_return:
    ret

codesg ends
end main
```