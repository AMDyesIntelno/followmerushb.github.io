### 外中断信息

在PC中,外中断源一共有两类

1. 可屏蔽中断

CPU是否响应可屏蔽中断,要看标志寄存器的`IF`位的设置

当CPU检测到可屏蔽的中断信息时,如果`IF=1`,则CPU在执行完当前指令后响应中断,引发中断过程,如果`IF=0`,则不响应可屏蔽中断

!>可屏蔽信息来自于CPU外部,中断类型码通过数据总线送入CPU

`sti`将IF设置为1

`cli`将IF设置为0

2. 不可屏蔽中断

不可屏蔽中断是CPU必须响应的外中断,不可屏蔽中断的中断类型码固定为`2`

不可屏蔽中断的中断过程为

- 标志寄存器入栈,IF=0,TF=0
- CS,IP入栈
- IP=`8`,CS=`0AH`

!>几乎所有外设引发的外中断都是可屏蔽中断,不可屏蔽中断是系统中有必须处理的紧急情况发生时用来通知CPU的中断信息

### PC机键盘处理过程

键盘上的每一个键相当于一个开关,键盘中有一个芯片对键盘上的每一个键的开关状态进行扫描

按下一个键时,开关接通,该芯片就产生一个扫描码,扫描码说明按下的键在键盘上的位置,扫描码被送入到端口地址为`60h`的寄存器中

松开按下的键时,也产生一个扫描码,送到相同的寄存器中

按下一个键产生的扫描码称为**通码**,松开产生的扫描码称为**断码**

扫描码长度为一个字节,通码的第7位为0,断码的第7位为1,即`断码=通码+80h`

当键盘的输入到达`60h`端口时,相关芯片会向CPU发出中断类型码为`9`的可屏蔽中断,CPU检测到该中断时,如果`IF=1`则响应中断,执行`int 9h`中断例程

BIOS键盘缓冲去存放`int 9h`例程接收的键盘输入的内存区,可以存储`15`个键盘输入

`int 9h`中断例程除了接收扫描码外还要产生和扫描码对应的字符码,一个键盘输入用一个字单元存放,高位字节存放扫描码,低位字节存放字符码




### 实验15 安装新的int9中断例程

?>在DOS下,按下`A`键后,除非不再松开,如果松开,就显示满屏幕的`A`

```nasm
assume cs:code
stack segment
    db 128 dup(0)
stack ends
code segment
main:
    mov ax,stack
    mov ss,ax
    mov sp,128

    push cs
    pop ds

    mov ax,0
    mov es,ax

    mov si,offset int9
    mov di,204h
    mov cx,offset int9end-offset int9
    cld
    rep movsb

    push es:[9*4]
    pop es:[200h]
    push es:[9*4+2]
    pop es:[202h]

    cli
    mov word ptr es:[9*4],204h
    mov word ptr es:[9*4+2],0
    sti

    mov ax,4c00h
    int 21h

int9:
    push ax
    push bx
    push cx
    push es

    in al,60h

    pushf
    call dword ptr cs:[200h]
    cmp al,9eh;A的断码
    jne int9ret

    mov ax,0b800h
    mov es,ax
    mov ah,00011100b
    mov al,'A'
    xor bx,bx
    mov cx,2000
s:
    mov word ptr es:[bx],ax
    add bx,2
    loop s


int9ret:
    pop es
    pop cx
    pop bx
    pop ax
    iret

int9end:
    nop

code ends
end main
```